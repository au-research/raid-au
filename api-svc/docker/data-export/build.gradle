import org.apache.tools.ant.filters.FixCrLfFilter

import static raido.OsUtil.osCmd

version = rootProject.version

// needed to reference ext properties of the project
evaluationDependsOn(':api-svc:spring')

ext{
  apiSvcJarPath = project(":api-svc:spring").tasks.uberJar.archivePath
  srcDir = 'src/main/docker'
  stagingDir = "$project.buildDir/dockerStaging"

  imageName = "raido-public-data-export"
  containerName = imageName

  /* image path we build for local use, nice and short and readable. */
  localImagePath = "$imageName:latest"
}

task dockerStage(type: Copy) {
  dependsOn ':api-svc:spring:build'
  group = 'docker.build'
  destinationDir = file(stagingDir)
  description = "copy dockerfile, scripts, jar, etc. to $stagingDir"

  doFirst {
    println "pulling application jar from $apiSvcJarPath"
    delete file(stagingDir)
  }

  // docker files
  from (srcDir){
    // avoid the chaos of windows line endings on unix
    // shouldn't need this on raido, given the text=lf git setting
    filter(FixCrLfFilter.class, eol: FixCrLfFilter.CrLf.newInstance("lf"))
  }

  from(apiSvcJarPath)
}

task dockerBuild(type: Exec){
  group = 'docker.build'
  dependsOn dockerStage
  description = "build the docker image"
  
  doFirst{
    println "building $localImagePath ..."
  }
  workingDir = project.ext.stagingDir
  
  commandLine osCmd('docker', 'build', 
    '-t', localImagePath, 
    '-f', 'Dockerfile', 
    '.'
  )
}


