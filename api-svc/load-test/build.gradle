import io.gatling.gradle.GatlingRunTask

plugins{
  id 'java'
  id 'io.gatling.gradle' version "3.9.3"
}
 
ext{
  defaultSysProps = [
    /* prod machines will always be set to UTC, but developer machines are set 
    to whatever they want - so this forces it for gradle stuff. */
    'user.timezone': 'UTC',
    // platform specific, so force it
    'file.encoding': 'UTF-8',
    // for consistency, rather than any specific reason 
    "user.language":"", "user.country":"", "user.variant":"",
  ]

  /* these are copied from doco, dunno if they're defaulted: 
  https://gatling.io/docs/gatling/reference/current/extensions/gradle_plugin/ */
  defaultJvmArgs = [
    '-server',
    '-Xmx1G',
    '-XX:+UseG1GC',
    '-XX:+ParallelRefProcEnabled',
    '-XX:MaxInlineLevel=20',
    '-XX:MaxTrivialSize=12',
  ]

  // the config loading config :/
  homeDir = System.properties['user.home']
}

repositories{
  mavenCentral()
}

version = rootProject.version

/* don't use groovy for prod code: dynamic typing, slow startup and poor 
   historical upgrade compatibility */
dependencies{
  gatling project(':api-svc:db:raido')
  gatling project(':api-svc:idl-raid-v2')
  gatling project(':api-svc:spring')

  gatling libs.bundles.spring6
  gatling libs.jooq
  
  gatling libs.bundles.slf4j

  gatlingRuntimeOnly 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
  gatling 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.14.2'
  gatling "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.14.2"

  gatling 'com.auth0:java-jwt:4.3.0'

  gatling "com.squareup.okhttp3:okhttp:4.10.0"
  
  // use logback as the backing log implementation for slf4j
  gatlingRuntimeOnly libs.bundles.logback.runtime

}

compileJava {
  options.compilerArgs <<
    // avoid warnings about spring classes using deprecated '-debug' 
    "-parameters" <<
    // annoy people into not doing unchecked shenanigans ðŸ˜’
    "-Xlint:unchecked" <<
    // annoy people into fixing their deprecation warnings 
    "-Xlint:deprecation"
}


// look for resources in the source tree, resources folder is pointless 
sourceSets.gatling.resources.srcDirs(sourceSets.gatling.java.srcDirs)

gatling {
  simulations = {
    include "raido/loadtest/simulation/Read.java"
  }

  logLevel = "WARN"
  jvmArgs = defaultJvmArgs
  systemProperties = defaultSysProps
}

tasks.register('gatlingRunDev', GatlingRunTask) {
  simulations = {
    include "raido/loadtest/simulation/Dev.java"
  }

  logLevel = "WARN"
  jvmArgs = defaultJvmArgs
  systemProperties = defaultSysProps
}