import org.apache.tools.ant.filters.FixCrLfFilter

import static raido.EcrUtil.getEcrLoginPassword
import static raido.OsUtil.osCmd

version = rootProject.version


ext{
  srcDir = 'src/main/docker'
  stagingDir = "$buildDir/dockerStaging"

  imageName = "raido-db-restore"
  containerName = imageName

  /* image path we build for local use, nice and short and readable. */
  localImagePath = "$imageName:latest"

  
}

task dockerStage(type: Copy) {
  group = 'docker.build'
  destinationDir = file(stagingDir)
  description = "copy dockerfile, scripts, etc. to $stagingDir"

  // docker files
  from (srcDir){
    // avoid the chaos of windows line endings on unix
    // shouldn't need this on raido, given the text=lf git setting
    filter(FixCrLfFilter.class, eol: FixCrLfFilter.CrLf.newInstance("lf"))
  }
}

task dockerBuild(type: Exec){
  group = 'docker.build'
  dependsOn dockerStage
  description = "build the docker image $containerName:[project version]"
  
  doFirst{
    println "building $localImagePath ..."
  }
  workingDir = project.ext.stagingDir
  commandLine osCmd('docker', 'build', '-t', localImagePath, '.')
}

