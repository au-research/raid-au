rootProject.name = "raido"

include ":api-svc:idl-raid-v1"
include ":api-svc:idl-raid-v2"
include ":api-svc:test-shared"
include ":api-svc:spring"
include ":api-svc:docker"
include ":api-svc:db"
include ":api-svc:db:shared"
include ":api-svc:db:raido"
include ":api-svc:db:v1-ddb-migration"
include ":api-svc:db:restore-raido-db"
include ":api-svc:load-test"
include ":app-client"

dependencyResolutionManagement{
  repositories {
    mavenCentral()
  }

  versionCatalogs{
    libs{
      /* https://www.jooq.org/learn/ 
      Follow relevant version link for patch release history. */
      version('jooq', '3.18.0')
      // https://github.com/spring-projects/spring-framework/releases
      version('spring6', '6.0.6')
      /* sometimes the latest version of spring-sec matches the spring version, 
      but often if lags behind, especially for fix versions.
      https://github.com/spring-projects/spring-security/releases */
      version('spring6-sec', '6.0.2')
      // https://github.com/eclipse/jetty.project/releases
      version('jetty11', '11.0.14')
      // https://logback.qos.ch/news.html
      version('logback', '1.4.6')
      // https://github.com/OpenFeign/feign/releases
      version('feign', '12.2')
      // https://www.slf4j.org/news.html
      version('slf4j', '2.0.7')


      // https://documentation.red-gate.com/fd/release-notes-for-flyway-engine-179732572.html
      plugin('flyway', 'org.flywaydb.flyway').version('9.19.3')      
      plugin('jooq', 'nu.studer.jooq').version('8.1')      
      
      library('jooq', 'org.jooq', 'jooq').versionRef('jooq')
      // https://jdbc.postgresql.org/changelogs/
      library('pgjdbc', 'org.postgresql:postgresql:42.5.4')
      // https://assertj.github.io/doc/#assertj-core-release-notes
      library('assertj', 'org.assertj:assertj-core:3.24.2')

      // core spring/web - pkg: org.springframework
      library('spring-webmvc', 'org.springframework', 'spring-webmvc').versionRef('spring6')
      // for @Transactional annotation
      library('spring-tx', 'org.springframework', 'spring-tx').versionRef('spring6')
      library('spring-jdbc', 'org.springframework', 'spring-jdbc').versionRef('spring6')
      // core spring/web security - pkg: import org.sf.security
      library('spring-security-core', 'org.springframework.security', 'spring-security-core').versionRef('spring6-sec')
      // pkg: org.sf.security.web
      library('spring-security-web', 'org.springframework.security', 'spring-security-web').versionRef('spring6-sec')
      // for configuring through code - pkg: org.sf.security.config
      library('spring-security-config', 'org.springframework.security', 'spring-security-config').versionRef('spring6-sec')
      library('spring-security-oauth2-core', 'org.springframework.security', 'spring-security-oauth2-core').versionRef('spring6-sec')
      library(
        'spring-security-oauth2-resource-server', 
        'org.springframework.security', 
        'spring-security-oauth2-resource-server'
      ).versionRef('spring6-sec')
      bundle('spring6', [
        'spring-webmvc', 
        'spring-tx',
        'spring-jdbc',
        'spring-security-core',
        'spring-security-web',
        'spring-security-config',
        'spring-security-oauth2-core',
        'spring-security-oauth2-resource-server',
      ])

      library('feign-okhttp', 'io.github.openfeign', 'feign-okhttp').versionRef('feign')
      library('feign-jackson', 'io.github.openfeign', 'feign-jackson').versionRef('feign')
      library('feign-slf4j', 'io.github.openfeign', 'feign-slf4j').versionRef('feign')
      bundle('feign', [
        'feign-okhttp',
        'feign-jackson',
        'feign-slf4j',
      ])

      // embedded Jetty server - pkg: org.eclipse.jetty.server
      library('jetty-server', 'org.eclipse.jetty','jetty-server').versionRef('jetty11')
      // Jetty support for Servlet API - pkg: org.eclipse.jetty.servlet
      library('jetty-servlets', 'org.eclipse.jetty','jetty-servlets').versionRef('jetty11')
      // new lib I had to import or couldnt' find ServletContextHandler
      library('jetty-servlet', 'org.eclipse.jetty','jetty-servlet').versionRef('jetty11')
      bundle('jetty11', ['jetty-server', 'jetty-servlets', 'jetty-servlet'])

      library('logback-core', 'ch.qos.logback','logback-core').versionRef('logback')
      library('logback-classic', 'ch.qos.logback','logback-classic').versionRef('logback')
      bundle('logback-runtime', ['logback-core', 'logback-classic'])

      library('spring-test', 'org.springframework', 'spring-test').versionRef('spring6')
      library('spring-security-test', 'org.springframework.security', 'spring-security-test').versionRef('spring6-sec')
      bundle('spring6-test', [ 'spring-test', 'spring-security-test'])
      
      library('slf4j-api', 'org.slf4j', 'slf4j-api').versionRef('slf4j')
      // allows SLF4J to capture java.util logging 
      library('slf4j-jul', 'org.slf4j', 'jul-to-slf4j').versionRef('slf4j')
      bundle('slf4j', [ 'slf4j-api', 'slf4j-jul'])

    }
  }
}

